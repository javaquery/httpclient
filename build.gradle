plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

sourceCompatibility = 1.8
group 'com.javaquery'
version '1.0.5'

repositories {
    mavenCentral()
}

dependencies {
    implementation('org.slf4j:slf4j-api:2.0.11')
    implementation 'org.json:json:20231013'
    implementation 'com.javaquery:util:1.2.3'

    implementation 'org.apache.httpcomponents:httpclient:4.5.13'
    implementation 'org.apache.httpcomponents:httpmime:4.5.13'

    implementation 'net.logstash.logback:logstash-logback-encoder:7.4'

    testImplementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.3.14'
    testImplementation('org.junit.jupiter:junit-jupiter:5.8.1')
}

test {
    useJUnitPlatform()
}

java {
    withJavadocJar()
    withSourcesJar()
}

jar {
    manifest {
        attributes(
                'Built-By': "Vicky Thakor"
        )
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'httpclient'
            from components.java

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Java Http Client'
                description = 'Java http client to extend interaction of http request'
                url = 'https://github.com/javaquery/httpclient'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://github.com/javaquery/httpclient/blob/main/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'javaquery'
                        name = 'Vicky Thakor'
                        email = 'vicky.thakor@javaquery.com'
                        timezone = '+05:30'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/javaquery/httpclient.git'
                    developerConnection = 'scm:git:ssh://github.com/javaquery/httpclient.git'
                    url = 'https://github.com/javaquery/httpclient'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username "javaquery"
                password "a2a@\$ASDF\$777"
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}